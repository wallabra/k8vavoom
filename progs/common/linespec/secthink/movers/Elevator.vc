//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2019 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class Elevator : SectorMover;

enum {
  ELEVEV_Down,
  ELEVEV_Up,
  ELEVEV_Current,
  ELEVEV_Raise,
  ELEVEV_Lower,
};

int Type;
int Direction;
float Speed;
float FloorDestDist;
float CeilingDestDist;
// so we would be able to backtrack elevator a little, otherwise the player will stuck
float FloorHeight;
float CeilingHeight;


//==========================================================================
//
//  Init
//
//==========================================================================
void Init (sector_t *InSector, int Arg1, int Arg2, int Arg3, int Arg4,
           int Arg5, int InType, line_t *Line)
{
  //float FloorHeight;
  //float CeilingHeight;
  float NewHeight;
  TVec Spot;

  Sector = InSector;
  Sector->FloorData = self;
  Sector->CeilingData = self;

  Type = InType;
  Speed = float(Arg2)*35.0/8.0;

  FloorHeight = GetPlanePointZ(ref Sector->floor, Sector->soundorg);
  CeilingHeight = GetPlanePointZ(ref Sector->ceiling, Sector->soundorg);

  // set up the fields according to the type of elevator action
  switch (Type) {
    // elevator down to next floor
    case ELEVEV_Down:
      Direction = -1;
      NewHeight = Level.FindNextLowestFloor(Sector, &Spot);
      Spot.z = NewHeight;
      FloorDestDist = DotProduct(Sector->floor.normal, Spot);
      Spot.z += GetPlanePointZ(ref Sector->ceiling, Spot)-GetPlanePointZ(ref Sector->floor, Spot);
      CeilingDestDist = DotProduct(Sector->ceiling.normal, Spot);
      break;
    // elevator up to next floor
    case ELEVEV_Up:
      Direction = 1;
      NewHeight = Level.FindNextHighestFloor(Sector, &Spot);
      Spot.z = NewHeight;
      FloorDestDist = DotProduct(Sector->floor.normal, Spot);
      Spot.z += GetPlanePointZ(ref Sector->ceiling, Spot)-GetPlanePointZ(ref Sector->floor, Spot);
      CeilingDestDist = DotProduct(Sector->ceiling.normal, Spot);
      break;
    // elevator to floor height of activating switch's front sector
    case ELEVEV_Current:
      Spot = *Line->v1;
      NewHeight = GetPlanePointZ(ref Line->frontsector->floor, Spot);
      Spot.z = NewHeight;
      FloorDestDist = DotProduct(Sector->floor.normal, Spot);
      Spot.z += GetPlanePointZ(ref Sector->ceiling, Spot)-GetPlanePointZ(ref Sector->floor, Spot);
      CeilingDestDist = DotProduct(Sector->ceiling.normal, Spot);
      Direction = (FloorDestDist > Sector->floor.dist ? 1 : -1);
      break;
    // elevate up by a specific amount
    case ELEVEV_Raise:
      Direction = 1;
      Spot = Sector->soundorg;
      Spot.z = FloorHeight+float(Arg3);
      FloorDestDist = DotProduct(Sector->floor.normal, Spot);
      Spot.z = CeilingHeight+float(Arg3);
      CeilingDestDist = DotProduct(Sector->ceiling.normal, Spot);
      break;
    // elevate down by a specific amount
    case ELEVEV_Lower:
      Direction = -1;
      Spot = Sector->soundorg;
      Spot.z = FloorHeight-float(Arg3);
      FloorDestDist = DotProduct(Sector->floor.normal, Spot);
      Spot.z = CeilingHeight-float(Arg3);
      CeilingDestDist = DotProduct(Sector->ceiling.normal, Spot);
      break;
  }

  StartFloorSound();
}


//==========================================================================
//
//  Tick
//
//==========================================================================
override void Tick (float DeltaTime) {
  int res;

  // if we're moving up, first should be the highest plane
  // if we're moving down, first should be the lowest plane

  bool floorIsFirst =
    Direction < 0 ?
      FloorHeight <= CeilingHeight : // moving down, floor is first if it is lower
      FloorHeight >= CeilingHeight; // moving up, floor is first if it is higher

  if (floorIsFirst) {
    // first, move floor
    res = MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:FloorDestDist, crush:0, floorOrCeiling:MOVE_PLANE_FLOOR, direction:Direction, stopOnCrush:false);
    if (res == RES_OK || res == RES_PASTDEST) {
      // ok, floor moved, move ceiling
      res = MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:CeilingDestDist, crush:0, floorOrCeiling:MOVE_PLANE_CEILING, direction:Direction, stopOnCrush:false);
      if (res == RES_CRUSHED) {
        // move floor back
        MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:FloorHeight, crush:0, floorOrCeiling:MOVE_PLANE_FLOOR, direction:-Direction, stopOnCrush:false);
      }
    }
  } else {
    // first, move ceiling
    res = MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:CeilingDestDist, crush:0, floorOrCeiling:MOVE_PLANE_CEILING, direction:Direction, stopOnCrush:false);
    if (res == RES_OK || res == RES_PASTDEST) {
      // ok, ceiling moved, move floor
      res = MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:FloorDestDist, crush:0, floorOrCeiling:MOVE_PLANE_FLOOR, direction:Direction, stopOnCrush:false);
      if (res == RES_CRUSHED) {
        // move ceiling back
        MovePlane(sector:Sector, amount:Speed*DeltaTime, dest:CeilingHeight, crush:0, floorOrCeiling:MOVE_PLANE_CEILING, direction:-Direction, stopOnCrush:false);
      }
    }
  }

  if (res == RES_PASTDEST) {
    // if destination height acheived
    // make floor stop sound
    SectorStopSequence(Sector);
    Finished(); // remove elevator from actives
  }
}


//==========================================================================
//
//  StartFloorSound
//
//==========================================================================
void StartFloorSound () {
  if (!LineSpecialLevelInfo(Level).DefaultFloorSound || Sector->seqType >= 0) {
    SectorStartSequence(Sector, GetSeqTrans(Sector->seqType, SEQ_Platform), 0);
  } else {
    SectorStartSequence(Sector, LineSpecialLevelInfo(Level). DefaultFloorSound, 0);
  }
}


defaultproperties {
}
