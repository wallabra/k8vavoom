//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************

//==========================================================================
//
//  IsInventoryCheckpointPossible
//
//==========================================================================
bool IsInventoryCheckpointPossible () {
  for (Inventory it = Inventory; it; it = it.Inventory) {
    if (!it.IsCheckpointSaveAllowed()) return false;
  }
  return true;
}


//==========================================================================
//
//  QS_ClearEntityInventory
//
//==========================================================================
override void QS_ClearEntityInventory () {
  DestroyAllInventory();
  if (bIsPlayer && Player && Player.MO == self) {
    PlayerEx plr = PlayerEx(Player);
    plr.SetWeapon(none);
    plr.BringUpWeapon(instant:true, skipSound:true);
  }
}


//==========================================================================
//
//  QS_GetEntityInventory
//
//  returns first item in inventory, or `none`
//
//==========================================================================
override Entity QS_GetEntityInventory () {
  return Inventory;
}


//==========================================================================
//
//  QS_SpawnEntityInventory
//
//  adds to current inventory
//
//==========================================================================
override Entity QS_SpawnEntityInventory (name className) {
  if (!className) return none;

  class!Inventory ec = class!Inventory(FindClass(className));
  if (!ec) return none;
  //ec = GetCompatibleClassReplacement(class!EntityEx, ec);
  //if (ec) ArmorClass = ec;

  Inventory ent = FindInventory(ec, disableReplacement:true);
  if (!ent) {
    EntityEx newEnt = Spawn(ec, AllowReplace:false);
    if (!newEnt) return none;
    ent = Inventory(newEnt);
    if (!ent) { delete newEnt; return none; }
    ent.AttachToOwner(self);
  }

  return ent;
}


//==========================================================================
//
//  DebugDumpInventory
//
//==========================================================================
void DebugDumpInventory () {
  printdebug("=== %C:%s inventory ===", self, UniqueId);
  for (Inventory inv = Inventory; inv; inv = inv.Inventory) {
    if (inv.Owner) {
      if (inv.Owner != self) {
        printdebug("  %C:%s (Owner=%C:%s; WTF?!)", inv, inv.UniqueId, inv.Owner, inv.Owner.UniqueId);
      } else {
        printdebug("  %C:%s", inv, inv.UniqueId);
      }
    } else {
      printdebug("  %C:%s (NO OWNER! WTF?!)", inv, inv.UniqueId);
    }
  }
  printdebug("-------------------");
}


//==========================================================================
//
//  AddInventory
//
//  this also process `bTransferInventory` flag
//
//==========================================================================
void AddInventory (Inventory Item) {
  if (!Item) return;

  /*
  if (Inventory(self)) {
    bool doDebug = (Item !isa 'LDWeaponNameAlternation');
    if (doDebug) printdebug("%C(%B): adding inventory item %C (owner=%C; bTransferInventory=%B)", self, Inventory(self).bTransferInventory, Item, Item.Owner, Item.bTransferInventory);
  }
  */

  if (Item.Owner == Item) {
    printerror("%C: AddInventory: Item `%C` owns itself, wtf?!", self, Item);
    Item.Owner = none;
    if (Item.Inventory == Item) Item.Inventory = none; // break a loop, just in case
  }

  if (Item == self) {
    printerror("%C: AddInventory: Item `%C` tries to became the inventory of itself, wtf?!", self, Item);
    if (Item.Owner) {
      assert(Item.Owner != self); // should be fixed above
      EntityEx(Item.Owner).RemoveInventory(Item);
      return;
    }
  }

  if (Item.Owner) {
    // already has an owner
    if (Item.Owner == self) return; // already in the inventory
    // remove from current owner's inventory
    //if (doDebug) printdebug("%C:   removing item `%C` from owner `%C`", self, Item, Item.Owner);
    EntityEx(Item.Owner).RemoveInventory(Item);
    // no need to check for inventory transfer flag, because freshly extracted item has no inventory
  } else if (Item.bTransferInventory) {
    // move item's inventory to our own inventory
    //if (doDebug) printdebug("%C:   transferring item `%C` inventory to self", self, Item);
    Item.TransferInventoryTo(self);
  } else {
    // item's inventory should be destroyed, we don't need it
    //if (doDebug && Item.Inventory) { printdebug("%C:   destroying item `%C` inventory", self, Item); Item.DebugDumpInventory(); }
    Item.DestroyInventory();
  }

  // invariants
  assert(Item.Inventory == none);
  assert(Item.Owner == none);

  //printdebug("%C:%s: got item `%C:%s`", self, UniqueId, Item, Item.UniqueId);

  // add it at the top of the inventory
  Item.Inventory = Inventory;
  Inventory = Item;

  // set item's owner
  Item.Owner = self;

  //DebugDumpInventory();
}


//==========================================================================
//
//  RemoveInventory
//
//==========================================================================
void RemoveInventory (Inventory Item) {
  if (!Item) return;

  if (Item.Owner == Item) {
    printerror("%C: RemoveInventory: Item `%C` owns itself, wtf?!", self, Item);
    Item.Owner = none;
    if (Item.Inventory == Item) Item.Inventory = none; // break a loop, just in case
  }

  if (Item == self) {
    printerror("%C: RemoveInventory: Item `%C` tries to remove itself from it's own inventory, wtf?!", self, Item);
    return;
  }

  if (!Item.Owner) {
    if (!Item.Inventory) return; // nothing to do
    printerror("%C: RemoveInventory: Item `%C` tries to remove itself from my inventory, and has inventory itself, wtf?!", self, Item);
    return;
  } else if (Item.Owner != self) {
    printwarn("%C: RemoveInventory: Item `%C` tries to remove itself from my inventory, but it is owned by `%C`; rerouting", self, Item, Item.Owner);
    EntityEx(Item.Owner).RemoveInventory(Item);
    return;
  }

  // find previous item or owner itself, if it's the first item
  EntityEx Prev = self;
  while (Prev) {
    EntityEx n = Prev.Inventory;
    if (n == Item) break;
    Prev = n;
  }

  if (!Prev) {
    if (Item.Owner) {
      if (Item.Owner != self) {
        printwarn("%C:%s: Cannot remove '%C:%s' from inventory of '%C': it is not there!", self, UniqueId, Item, Item.UniqueId, self);
        printwarn(" OWNER: '%C'", Item.Owner);
        Item.DetachedFromOwner();
      } else {
        if (IsDestroyed) {
          //printdebug("%C:%s: Cannot remove '%C:%s' from inventory of '%C': it is not there!", self, UniqueId, Item, Item.UniqueId, self);
          //printdebug(" OWNER: SELF (dead)");
        } else {
          printwarn("%C:%s: Cannot remove '%C:%s' from inventory of '%C': it is not there!", self, UniqueId, Item, Item.UniqueId, self);
          printwarn(" OWNER: SELF");
        }
      }
    }
    Item.Owner = none;
    Item.Inventory = none;
  } else {
    // unlink and remove owner
    //printdebug("%C:%s: removed '%C:%s' from my inventory", self, UniqueId, Item, Item.UniqueId);
    Prev.Inventory = Item.Inventory;
    Item.Inventory = none;
    Item.DetachedFromOwner();
    Item.Owner = none;
    //DebugDumpInventory();
  }
}


//==========================================================================
//
//  FindInventory
//
//==========================================================================
final Inventory FindInventory (class!Inventory ItemClass, optional bool disableReplacement) {
  if (!ItemClass) return none;
  class!Inventory repl = class!Inventory(GetClassReplacement(ItemClass));

  for (Inventory Check = Inventory; Check; Check = Check.Inventory) {
    if (Check.Class == ItemClass) return Check;
    // consider replacements too
    if (!disableReplacement && GetClassReplacement(Check.Class) == repl) {
      //printdebug("FOUND REPLACEMENT INVENTORY: ItemClass=<%C>; repl=<%C>; Check=<%C>", ItemClass, repl, Check);
      return Check;
    }
  }

  return none;
}


//==========================================================================
//
//  FindInfiniteAmmoPowerup
//
//==========================================================================
final PowerInfiniteAmmo FindInfiniteAmmoPowerup () {
  for (auto inv = Inventory; inv; inv = inv.Inventory) {
    auto pwi = PowerInfiniteAmmo(inv);
    if (pwi) return pwi;
  }
  return none;
}


//==========================================================================
//
//  FindBuddhaPowerup
//
//==========================================================================
final PowerBuddha FindBuddhaPowerup () {
  for (auto inv = Inventory; inv; inv = inv.Inventory) {
    auto pup = PowerBuddha(inv);
    if (pup) return pup;
  }
  return none;
}


//==========================================================================
//
//  FindRegenerationPowerup
//
//==========================================================================
final PowerRegeneration FindRegenerationPowerup () {
  for (auto inv = Inventory; inv; inv = inv.Inventory) {
    auto pup = PowerRegeneration(inv);
    if (pup) return pup;
  }
  return none;
}


//==========================================================================
//
//  ChooserCheckAllowedReplacements
//
//  check if this is a weapon, and replace it if we have a replacement
//
//  this is so `GiveInventory()` with weapon types gives the correct
//  ammo/weapon type with weapon mods
//
//==========================================================================
static final class!Inventory ChooserCheckAllowedReplacements (class!Inventory Type) {
  if (class!Weapon(Type)) {
    class!Inventory repl = class!Inventory(GetClassReplacement(Type));
    if (repl != Type && class!Weapon(repl) && !class!RandomSpawner(repl)) {
      printdebug("ChooserCheckAllowedReplacements: weapon %C replaced with %C", Type, repl);
      Type = repl;
    }
  } else if (class!Ammo(Type)) {
    class!Inventory repl = class!Inventory(GetClassReplacement(Type));
    if (repl != Type && class!Ammo(repl) && !class!RandomSpawner(repl)) {
      printdebug("ChooserCheckAllowedReplacements: ammo %C replaced with %C", Type, repl);
      Type = repl;
    }
  }
  return Type;
}


//==========================================================================
//
//  ChooseInventoryType
//
//  use this instead of directly spawning an inventory item
//
//==========================================================================
static final class!Inventory ChooseInventoryType (class!EntityEx Type, bool disableReplace) {
  if (!Type) return none;

       if (Type isa 'BasicArmor') Type = BasicArmor;
  else if (Type isa 'HexenArmor') Type = HexenArmor;

  class!EntityEx replCls = (disableReplace ? Type : class!EntityEx(GetClassReplacement(Type)));
  if (!replCls) replCls = Type;

  if (class!RandomSpawner(replCls)) {
    class!Inventory invCls = class!Inventory(ChooseFromDropItemList(replCls, AllowReplace:!disableReplace));
    if (!invCls) return none;
    name itpname = 'inventory';
         if (invCls isa 'Ammo') itpname = 'ammo';
    else if (invCls isa 'Weapon') itpname = 'weapon';
    printdebug("RandomSpawner `%C` chose %s item `%C`", replCls, itpname, invCls);
    Type = invCls;
  }

  return ChooserCheckAllowedReplacements(class!Inventory(Type));
}


//==========================================================================
//
//  ChooseInventoryTypeByName
//
//  use this instead of directly spawning an inventory item
//
//==========================================================================
static final class!Inventory ChooseInventoryTypeByName (name ItemName, bool disableReplace) {
  if (!ItemName || nameicmp(ItemName, 'none') == 0) return none;
  if (nameicmp(ItemName, 'armor') == 0) ItemName = 'basicarmor';
  if (nameicmp(ItemName, 'basicarmor') == 0) return ChooseInventoryType(BasicArmor, disableReplace);
  if (nameicmp(ItemName, 'hexenarmor') == 0) return ChooseInventoryType(HexenArmor, disableReplace);
  return ChooseInventoryType(class!EntityEx(FindClassNoCase(ItemName)), disableReplace);
}


//==========================================================================
//
//  SpawnInventoryTypeByName
//
//==========================================================================
static final Inventory SpawnInventoryTypeByName (Thinker spwnr, name ItemName, optional bool disableReplace) {
  class!Inventory Type = ChooseInventoryTypeByName(ItemName, disableReplace:disableReplace!optional);
  if (!Type) return none;
  //k8: if item's pickup code calls some movement functions, this may trigger
  //    another call to pickup handler (due to something bumped into new item),
  //    and it will go on, and on, and on (thanks to TDRR for finding this!).
  //    so i will move the item far away, and let's hope that there will be nobody there
  Inventory inv = spwnr.SpawnEntityChecked(class!Inventory, Type, vector(-33000, -33000, -33000), AllowReplace:false);
  if (!Inventory(inv)) { delete inv; inv = none; }
  return inv;
}


//==========================================================================
//
//  SpawnInventoryTypeChecked
//
//==========================================================================
static final spawner Inventory SpawnInventoryTypeChecked (class!Inventory destType, Thinker spwnr, class!EntityEx Type, optional bool disableReplace) {
  if (!Type || !destType) return none;
  class!Inventory stype = ChooseInventoryType(Type, disableReplace);
  if (!stype) return none;
  if (!destType(stype)) {
    // alas
    printwarn("class `%C` is not a subclass of `%C`, cannot spawn inventory", stype, destType);
    return none;
  }
  //k8: if item's pickup code calls some movement functions, this may trigger
  //    another call to pickup handler (due to something bumped into new item),
  //    and it will go on, and on, and on (thanks to TDRR for finding this!).
  //    so i will move the item far away, and let's hope that there will be nobody there
  Inventory inv = spwnr.SpawnEntityChecked(destType, stype, vector(-33000, -33000, -33000), AllowReplace:false);
  if (inv && !destType(inv.Class)) {
    printwarn("spawned item `%C` is not a subclass of `%C`, discarding", inv, destType);
    delete inv;
    inv = none;
  }
  return inv;
}


//==========================================================================
//
//  SpawnInventoryType
//
//==========================================================================
static final Inventory SpawnInventoryType (Thinker spwnr, class!EntityEx Type, optional bool disableReplace) {
  return SpawnInventoryTypeChecked(class!Inventory, spwnr, Type, disableReplace!optional);
}


//==========================================================================
//
//  SpawnAmmoType
//
//==========================================================================
static final Ammo SpawnAmmoType (Thinker spwnr, class!Ammo Type, optional bool disableReplace) {
  return SpawnInventoryTypeChecked(class!Ammo, spwnr, Type, disableReplace!optional);
}


//==========================================================================
//
//  SpawnWeaponType
//
//==========================================================================
static final Weapon SpawnWeaponType (Thinker spwnr, class!Weapon Type, optional bool disableReplace) {
  return SpawnInventoryTypeChecked(class!Weapon, spwnr, Type, disableReplace!optional);
}


//==========================================================================
//
//  GiveInventoryType
//
//==========================================================================
final Inventory GiveInventoryType (class!Inventory Type, optional bool disableReplace/*=false*/) {
  if (!Type) return none;
  Inventory Item = SpawnInventoryType(self, Type, disableReplace:disableReplace);
  if (!Item) return none;
  if (!Inventory(Item)) {
    printwarn("WUTAFUCK with GiveInventoryType? (%C)", Item);
    delete Item;
    return none;
  }
  // this shouldn't count for the item statistics
  if (Item.bCountItem) {
    Item.bCountItem = false;
    --Level.TotalItems;
  }
  Item.bDropped = true;
  //printdebug("*** Item=%C (%C)", Item, self);
  if (!Item.TryPickup(self)) {
    Item.Destroy();
    return none;
  }
  return Item;
}


//==========================================================================
//
//  DestroyAllInventory
//
//==========================================================================
final void DestroyAllInventory () {
  while (Inventory) Inventory.Destroy();
}


//==========================================================================
//
//  UseInventory
//
//==========================================================================
bool UseInventory (Inventory Item) {
  // don't use items if you are dead
  if (Health <= 0) return false;
  // don't use item if don't actually have it
  if (Item.Amount <= 0) return false;
  // tell item that it is used
  if (!Item.Use(false)) return false;
  // item was used, remove it from inventory
  --Item.Amount;
  if (Item.Amount <= 0 && !Item.bKeepDepleted) Item.Destroy();
  return true;
}


//==========================================================================
//
//  DropInventory
//
//  -1 means "one"
//
//==========================================================================
EntityEx DropInventory (Inventory SrcItem, optional int DropAmount) {
  Inventory Item = SrcItem.CreateTossable(DropAmount!optional);
  if (Item) {
    TVec Dir;
    AngleVector(Angles, out Dir);
    Dir.z = 0.0;
    Dir = Normalise(Dir);
    Item.SetOrigin2(Origin+vector(0.0, 0.0, 32.0)+Dir*(Radius+32.0), properFloorCheck:true);
    Item.Angles.yaw = Angles.yaw;
    Item.Velocity = Dir*128.0;
  }
  return Item;
}


//==========================================================================
//
//  ObtainInventory
//
//==========================================================================
void ObtainInventory (EntityEx Other) {
  if (!Other) return; //k8: just in case

  // actor should not have any inventory
  if (Inventory) Error("ObtainInventory called while still having an inventory");

  Inventory = Other.Inventory;
  Other.Inventory = none;
  Inventory Item;
  for (Item = Inventory; Item; Item = Item.Inventory) Item.Owner = self;
}


//===========================================================================
//
//  ClearInventory
//
//===========================================================================
override void ClearInventory () {
  Inventory Item = Inventory;
  while (Item) {
    Inventory Next = Item.Inventory;
    if (!Item.bUndroppable) {
      // IMHO any item that should be kept should not be dropped, not just ammo
      if (Item.bKeepDepleted) {
        Item.Amount = 0;
      } else {
        Item.Destroy();
      }
    } else if (HexenArmor(Item)) {
      HexenArmor A = HexenArmor(Item);
      A.Slots[0] = 0.0;
      A.Slots[1] = 0.0;
      A.Slots[2] = 0.0;
      A.Slots[3] = 0.0;
    }
    Item = Next;
  }

  if (bIsPlayer) {
    PlayerEx(Player).ReadyWeapon = none;
    PlayerEx(Player).PendingWeapon = none;
    Player.SetViewObject(none);
    Player.SetViewState(PS_WEAPON, none);
    Player.SetViewState(PS_FLASH, none);
  }
}


//===========================================================================
//
//  GiveInventory
//
//  called from ACS
//
//===========================================================================
override void GiveInventory (name ItemName, int Amount, bool allowReplacement) {
  //printwarn("*** %C: GiveInventory: %s %s", ItemName, Amount);
  if (Amount <= 0) return;
  if (!ItemName || nameicmp(ItemName, 'none') == 0) return;

  Inventory Item = SpawnInventoryTypeByName(self, ItemName, disableReplace:!allowReplacement);
  if (!Item) {
    printwarn("GiveInventory: Unknown item type '%s'", ItemName);
    return;
  }

  Weapon SavedPendingWeapon = none;
  bool HadWeapon = true;
  if (bIsPlayer) {
    SavedPendingWeapon = PlayerEx(Player).PendingWeapon;
    HadWeapon = !!PlayerEx(Player).ReadyWeapon;
  }

  // this shouldn't count for the item statistics
  if (Item.bCountItem) {
    Item.bCountItem = false;
    --Level.TotalItems;
  }
       if (BasicArmorPickup(Item)) BasicArmorPickup(Item).SaveAmount *= Amount;
  else if (BasicArmorBonus(Item)) BasicArmorBonus(Item).SaveAmount *= Amount;
  else Item.Amount = Amount;

  if (!Item.TryPickup(self)) {
    Item.Destroy();
  } else if (!Item.IsDestroyed) {
    // just in case, move it to spawner's origin
    Item.SetOrigin(Origin, properFloorCheck:true);
  }

  // don't automatically bring up weapon
  if (bIsPlayer && HadWeapon) {
    PlayerEx(Player).PendingWeapon = SavedPendingWeapon;
  }
}


//===========================================================================
//
//  FindInventoryClassForACS
//
//===========================================================================
final class!Inventory FindInventoryClassForACS (name ItemName, bool allowReplacement) {
  if (!ItemName || nameicmp(ItemName, 'none') == 0) return none;
  if (nameicmp(ItemName, 'armor') == 0) ItemName = 'basicarmor';
  if (nameicmp(ItemName, 'basicarmor') == 0) return BasicArmor;
  if (nameicmp(ItemName, 'hexenarmor') == 0) return HexenArmor;
  class!EntityEx ecls = class!EntityEx(FindClassNoCase(ItemName));
  if (!ecls) return none;
  if (allowReplacement) {
    class!EntityEx repl = class!EntityEx(GetClassReplacement(ecls));
    if (repl) ecls = repl;
  }
  return class!Inventory(ecls);
}


//===========================================================================
//
//  FindInventoryByTypeForACS
//
//===========================================================================
final Inventory FindInventoryByTypeForACS (class!Inventory ItemClass, bool allowReplacement) {
  if (!ItemClass || !class!Inventory(ItemClass)) return none;
  Inventory Item = FindInventory(ItemClass, disableReplacement:!allowReplacement);
  if (Item) return Item;
  if (allowReplacement) return none; // replacements already checked
  // if this is any weapon or ammo, check for replacements, for weapon mods
  class!EntityEx replCls;
       if (class!Weapon(ItemClass)) replCls = class!Weapon(GetClassReplacement(ItemClass));
  else if (class!Ammo(ItemClass)) replCls = class!Ammo(GetClassReplacement(ItemClass));
  else return none; // alas
  if (!replCls || replCls == ItemClass) return none;
  // try to find replaced inventory
  return FindInventory(ItemClass, disableReplacement:true);
}


//===========================================================================
//
//  TakeInventory
//
//===========================================================================
override void TakeInventory (name ItemName, int Amount, bool allowReplacement) {
  //printwarn("*** TakeInventory: %s %s", ItemName, Amount);
  if (Amount <= 0) return;

  class!Inventory ItemClass = FindInventoryClassForACS(ItemName, allowReplacement);
  if (!ItemClass) return;

  Inventory Item = FindInventoryByTypeForACS(ItemClass, allowReplacement:allowReplacement);
  if (!Item) return;

  Item.Amount -= Amount;
  if (Item.Amount <= 0) {
    if (Item.bKeepDepleted) {
      Item.Amount = 0;
    } else {
      Item.Destroy();
    }
  }
}


//===========================================================================
//
//  CheckInventory
//
//===========================================================================
override int CheckInventory (name ItemName, bool allowReplacement) {
  //printwarn("*** CheckInventory: %s", ItemName);
  class!Inventory ItemClass = FindInventoryClassForACS(ItemName, allowReplacement);
  if (!ItemClass) return 0;
  Inventory Item = FindInventoryByTypeForACS(ItemClass, allowReplacement:allowReplacement);
  return (Item ? Item.Amount : 0);
}


//===========================================================================
//
//  UseInventoryName
//
//===========================================================================
override int UseInventoryName (name ItemName, bool allowReplacement) {
  class!Inventory ItemClass = FindInventoryClassForACS(ItemName, allowReplacement);
  if (!ItemClass) return 0;
  Inventory Item = FindInventoryByTypeForACS(ItemClass, allowReplacement:allowReplacement);
  return (Item ? UseInventory(Item) : 0);
}


//===========================================================================
//
//  GetArmorPoints
//
//===========================================================================
override int GetArmorPoints () {
  Inventory A = FindInventory(BasicArmor, disableReplacement:true);
  return (A ? A.Amount : 0);
}


//===========================================================================
//
//  GetArmorPointsForType
//
//===========================================================================
override int GetArmorPointsForType (name armtype) {
  if (!armtype || nameicmp(armtype, 'None') == 0) return 0;
  BasicArmor armor = BasicArmor(FindInventory(BasicArmor, disableReplacement:true));
  if (!armor) return 0;
  //FIXME: should we really try parents here?
  if (nameicmp(armtype, armor.GetArmorName()) == 0) return max(0, armor.Amount);
  /*
  if (armor.GetArmorName() == 'None') return 0;
  class!Inventory aclass = armor.ArmorClass;
  while (aclass) {
    if (nameicmp(armtype, GetClassName(aclass)) == 0) return max(0, armor.Amount);
    aclass = class!Inventory(GetClassParent(aclass));
  }
  */
  return 0;
}


//===========================================================================
//
//  CheckNamedWeapon
//
//  used from ACS
//
//===========================================================================
override int CheckNamedWeapon (name Name) {
  if (!bIsPlayer || !Name) return 0;
  Weapon wpn = PlayerEx(Player).ReadyWeapon;
  if (!wpn) return 0;
  if (nameicmp(Name, GetClassName(wpn.Class)) == 0) return 1;
  // check for replaced weapon, in case some weapon mod replaced it
  class!Weapon wpnCls = class!Weapon(FindClassNoCase(Name));
  if (!wpnCls) return 0; // this name is not a weapon name
  // get replacement
  class!Weapon replCls = class!Weapon(GetClassReplacement(wpnCls));
  if (!replCls || replCls == wpnCls) return 0; // this weapon has no replacement
  // is replaced weapon a current one?
  if (wpn.Class != replCls) return 0; // no, nothing to do here
  // check if our inventory doesn't contain the exactly named weapon
  // (this is so exact checks will work)
  for (Inventory inv = Inventory; inv; inv = inv.Inventory) {
    if (Weapon(inv)) {
      if (nameicmp(Name, GetClassName(inv.Class)) == 0) return 0; // alas, exactness check failed
    }
  }
  // ok, replaced weapon is good
  return 1;
}


//===========================================================================
//
//  SetNamedWeapon
//
//  used from ACS
//  if there is no such weapon, do nothing
//
//===========================================================================
override int SetNamedWeapon (name Name) {
  if (!bIsPlayer || !Name) return 0;

  class!Weapon wpnCls = class!Weapon(FindClassNoCase(Name));
  if (!wpnCls) return 0; // not a weapon name

  // check if it's already active
  if (PlayerEx(Player).ReadyWeapon && PlayerEx(Player).ReadyWeapon.Class == wpnCls) {
    // make sure player doesn't switch away from it
    PlayerEx(Player).PendingWeapon = none;
    return true;
  }

  /*
  Weapon Wpn = Weapon(FindInventory(wpnCls, disableReplacement:true));
  if (!Wpn) {
    // try replacement weapon, in case some weapon mod replaced it
    Wpn = Weapon(FindInventory(wpnCls, disableReplacement:false));
    if (!Wpn) return 0; // no such weapon
  }
  */

  Weapon Wpn = Weapon(FindInventoryByTypeForACS(wpnCls, allowReplacement:false));
  if (!Wpn) return false;

  // check if it's already active
  if (PlayerEx(Player).ReadyWeapon == Wpn) {
    // make sure player doesn't switch away from it
    PlayerEx(Player).PendingWeapon = none;
    return true;
  }

  // check if weapon has enough ammo
  if (!Wpn.CheckAmmo(Weapon::FIRE_Either, AutoSwitch:false)) return false;

  PlayerEx(Player).PendingWeapon = Wpn;
  return true;
}


//===========================================================================
//
//  GetAmmoCapacity
//
//  all types derived from Inventory but not from Ammo will return 0
//
//===========================================================================
override int GetAmmoCapacity (name Name) {
  class!Ammo AmmoType = class!Ammo(FindClassNoCase(Name));
  if (!AmmoType) return 0;

  // only direct descendants of Ammo class (k8: why?)
  //if (GetClassParent(AmmoType) != Ammo) return 0;

  // if we have this ammo, return current max amount, otherwise return default for this ammo type
  /*
  Ammo ammo = Ammo(FindInventory(AmmoType, disableReplacement:true));
  if (!ammo) {
    // try replacement ammo, in case some weapon mod replaced it
    ammo = Ammo(FindInventory(AmmoType, disableReplacement:false));
    if (!ammo) {
      // temporarily spawn it, so we can return amount according to "ammo king" mode
      ammo = SpawnEntityChecked(class!Ammo, AmmoType, AllowReplace:true);
      if (!ammo) return AmmoType.default.MaxAmount;
      int res = ammo.k8GetAmmoKingMax();
      delete ammo;
      return res;
    }
  }
  */

  Ammo ammo = Ammo(FindInventoryByTypeForACS(AmmoType, allowReplacement:false));
  if (!ammo) {
    // temporarily spawn it, so we can return amount according to "ammo king" mode
    ammo = SpawnAmmoType(self, AmmoType, disableReplace:true);
    if (!ammo) return AmmoType.default.MaxAmount;
    int res = ammo.k8GetAmmoKingMax();
    delete ammo;
    return res;
  }

  return ammo.k8GetAmmoKingMax();
}


//===========================================================================
//
//  SetAmmoCapacity
//
//===========================================================================
override void SetAmmoCapacity (name Name, int Amount) {
  class!Ammo AmmoType = class!Ammo(FindClassNoCase(Name));
  if (!AmmoType) return;

  // only direct descendants of Ammo class (k8: why?)
  //if (GetClassParent(AmmoType) != Ammo) return 0;

  // make sure new ammo capacity is not negative
  Amount = max(0, Amount);

  /*
  Ammo ammo = Ammo(FindInventory(AmmoType, disableReplacement:true));
  if (!ammo) {
    // try replacement ammo, in case some weapon mod replaced it
    ammo = Ammo(FindInventory(AmmoType, disableReplacement:false));
    if (!ammo) {
      // give player this ammo, but make it empty, otherwise we cannot set the capacity
      ammo = Ammo(GiveInventoryType(AmmoType));
      if (!ammo) {
        printwarn("cannot give ammo of type `%C` (%s)", AmmoType, Name);
        return;
      }
      ammo.Amount = 0;
    }
  }
  */

  Ammo ammo = Ammo(FindInventoryByTypeForACS(AmmoType, allowReplacement:false));
  if (!ammo) {
    // give player this ammo, but make it empty, otherwise we cannot set the capacity
    Inventory inv = GiveInventoryType(AmmoType, disableReplace:true);
    if (inv) {
      ammo = Ammo(inv);
      if (!ammo) delete inv;
    }
    if (!ammo) {
      printwarn("cannot give ammo of type `%C` (%s)", AmmoType, Name);
      return;
    }
    ammo.Amount = 0;
  }

  ammo.MaxAmount = Amount;

  // we also should make sure that the current amount doesn't exceed the new limit
  ammo.Amount = min(ammo.Amount, ammo.k8GetAmmoKingMax());
}


//===========================================================================
//
//  FindActivePowerupTime
//
//===========================================================================
override float FindActivePowerupTime (name className) {
  //EntityEx tgt = EntityEx(DecorDoAAPtr('FindActivePowerupTime', aptr!optional));
  //if (!tgt) return 0;

  if (!className) return 0;
  class!Powerup ptype = class!Powerup(FindClassNoCase(className));
  if (!ptype) return 0;
  class!Powerup ptyperepl = class!Powerup(GetClassReplacement(ptype));

  for (auto Item = self.Inventory; Item; ) {
    Inventory Next = Item.Inventory;
    Powerup pup = Powerup(Item);
    if (pup) {
      if (ptyperepl && pup.Class == ptyperepl) return fmax(0, pup.EffectTime);
      if (pup.Class == ptype) return fmax(0, pup.EffectTime);
    }
    Item = Next;
  }

  return 0;
}
