//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2019 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, either version 3 of the License, or
//**  (at your option) any later version.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
// this is base pathfinding for bots
//
// we don't need to build any navigational mesh, as we arealdy have one!
// really, we were dropping breadcrumbs into each subsector center, but
// we don't need those breadcumbs, we can use subsectors themselves.
//
// there is some overhead in checking subsector segs, though.
// we can cache this info in Level object later.
//**************************************************************************
#define BOT_PATHER_FORCE_FULL_REFRESH

// A-Star node
class BotPathNode : MiAStarNodeBase;

// created for each passable seg
struct Transition {
  GameObject::seg_t *seg;
  GameObject::subsector_t *sub;
  int subnum;
  float dist; // <0: impassable
  float moveCost; // +20 for "needUse", arbitrary weight; <0: impassable
  bool needJump;
  bool needUse;
  // compare this with sector's values to see if we need to reevaluate "passability"
  float floorDist, ceilDist;
}

Level XLevel; // level object (i.e. actual map)
TVec Origin;
int index; // subsector number; also, node index in pather `nodes`
GameObject::subsector_t *sub;
bool door; // is this subsector a door part?
bool passable;

int queryCounter;

// reachable subsectors (should be at least one, zero means "don't know yet")
array!Transition exits;
//array!int sectorExits; // indicies in `exits` that leads to other sectors

// `true` if this node has some impassable 2-sided walls
// this means that this node should be re-evaluated next time the bot hits it
// this is done so the bot can notice opened secret doors like one on MAP01, for example
float myFloorDist, myCeilDist;


static final BotPathNode CreateNew (Level axlevel) {
  BotPathNode res = SpawnObject(BotPathNode);
  res.XLevel = axlevel;
  return res;
}


final bool isSectorChanged () {
  auto sec = sub.sector;
  return (sec.floor.dist != myFloorDist || sec.ceiling.dist != myCeilDist);
}


defaultproperties {
}


// ////////////////////////////////////////////////////////////////////////// //
class BotPathGraph : MiAStarGraphBase;

BotPlayerBase bot;

// a-star nodes
array!BotPathNode nodes;
// this flag will be set if some node was refreshed
// we need to reset cache, and look for path again

int queryCounter;
// nodes that need to be fully refreshed
// key: node index, value: nobody cares
dictionary!(int, int) refreshMap;

bool debugDump;


override void Destroy () {
  foreach (ref auto node; nodes) delete node;
  nodes.length = 0;
  ::Destroy();
}


final void IncQueryCounter () {
  if (!(++queryCounter)) {
    foreach (auto node; nodes) node.queryCounter = 0;
    queryCounter = 1;
  }
  if (refreshMap.length != 0) refreshAllNodesInMap();
#ifdef BOT_PATHER_FORCE_FULL_REFRESH
  foreach (auto node; nodes) {
    if (node.isSectorChanged) {
      print("FUCK! node #%d changed, but not queued!", node.index);
      node.queryCounter = 0;
      refreshNode(node);
      Reset();
    }
  }
#endif
}


final bool needOpenDoor (GameObject::sector_t *sec) {
  return (sec.ceiling.minz-sec.floor.maxz < bot.player.MO.Height);
}


final bool isDoorSector (GameObject::sector_t *sec, GameObject::subsector_t *sub) {
  // check if this sector has door activation lines
  GameObject::line_t *ld;
  //print("checking sector #%s for 'doorness' (%d lines; sub #%d)...", sec-&bot.player.MO.XLevel.Sectors[0], sec.lines.length, sub-&bot.player.MO.XLevel.Subsectors[0]);
  foreach (int idx, ld; sec.lines) {
    if (EntityEx.isGoodDoorLinedef(sec, ld)) return true;
  }
  return false;
}


// prerequisite: sector must be a door
// returns `true` if we need to restore a sector
final void fakeOpenDoor (BotPathNode node, out EntityEx::FakeDoorInfo fi) {
  if (node.door) {
    auto sec = node.sub.sector;
    auto lowest = EntityEx.findLowestCeilingSurrounding(sec);
    if (!lowest) { fi.saved = false; return; }
    EntityEx(bot.player.MO).fakeSaveOpenDoor(out fi, sec, lowest);
  } else {
    fi.saved = false;
  }
}


final void restoreFakeOpenDoor (const ref EntityEx::FakeDoorInfo fi) {
  EntityEx(bot.player.MO).restoreFakeOpenDoor(fi);
}


final bool IsGoodMapPosition (ref TVec pos) {
  Entity::tmtrace_t tmtrace;
  pos.z = EntityEx::ONFLOORZ;
  auto botmo = bot.player.MO;
  // increase radius a little, so exact passages won't confuse the bot
  auto oldRad = botmo.Radius;
  scope(exit) botmo.Radius = oldRad;
  botmo.Radius += 3;
  if (botmo.CheckRelPosition(&tmtrace, pos, noPickups:true, ignoreMonsters:true, ignorePlayers:true)) {
    pos = tmtrace.End;
    return true;
  }
  return false;
}


final void queueRefreshNode (BotPathNode node, optional bool forced) {
  if (!forced) {
    if (node.queryCounter == queryCounter) return;
    //print("queueRefreshNode(0): #%d", node.index);
    if (!node.isSectorChanged) return;
    //print("queueRefreshNode(1): #%d", node.index);
  } else {
    node.queryCounter = 0;
  }
  if (refreshMap.find(node.index)) return;
  print("queueRefreshNode(2): #%d", node.index);
  // queue this node, and all adjacent nodes
  print("queuing node #%d (sector #%d) for refresh", node.index, node.sub.sector-&bot.player.MO.XLevel.Sectors[0]);
  refreshMap.put(node.index, true);
  //node.queryCounter = queryCounter;
}


// should be called when sector heights changed
final void refreshNovePassability (BotPathNode node) {
  if (node.queryCounter == queryCounter) return; // already processed

  node.queryCounter = queryCounter;

  auto sec = node.sub.sector;

  node.myFloorDist = sec.floor.dist;
  node.myCeilDist = sec.ceiling.dist;

  EntityEx::FakeDoorInfo savedPlane;
  Level lev = bot.player.MO.XLevel;

  print("updating sub #%d (sector #%d) passability (old is %B)", node.index, node.sub.sector-&lev.Sectors[0], node.passable);

  fakeOpenDoor(node, out savedPlane);
  scope(exit) restoreFakeOpenDoor(savedPlane);

  auto sub = node.sub;
  TVec cpos = lev.GetSubsectorCenter(sub);
  bool oldPassable = node.passable;

  // check map position
  if (!IsGoodMapPosition(ref cpos)) {
    // cannot stand in subsector center, try corners
    node.passable = false;
    TVec center = lev.GetSubsectorCenter(sub);
    foreach (auto lidx; sub.firstline..sub.firstline+sub.numlines) {
      auto seg = &lev.Segs[lidx];
      TVec dir = center-(*seg->v1);
      if (dir.length2DSquared < 4*4) continue;
      cpos = (*seg->v1)+(dir.normalise2D)*3.5;
      if (IsGoodMapPosition(ref cpos)) {
        auto dss = lev.PointInSubsector(cpos);
        if (dss == sub) {
          node.passable = true;
          node.Origin = cpos;
          break;
        }
      }
    }
  } else {
    // can stand in subsector center
    node.passable = true;
    node.Origin = cpos;
  }

  if (node.passable != oldPassable) {
    print("node #%d becomes %svalid (door:%B; sector: %s)", node.index, (node.passable ? "" : "in"), node.door, node.sub.sector-&lev.Sectors[0]);
  }
}


// add adjacent nodes here
transient dictionary!(int, int) twoWayRefresh;

// should be called when sector heights changed
final void refreshNodeExit (BotPathNode node, int exitnum) {
  EntityEx botmo = EntityEx(bot.player.MO);
  Level lev = botmo.XLevel;
  auto tr = &node.exits[exitnum];

  auto sec = tr.sub.sector;
  //if (sec.floor.dist == tr.floorDist && sec.ceiling.dist == tr.ceilDist) return;

  tr.floorDist = sec.floor.dist;
  tr.ceilDist = sec.ceiling.dist;

  // ok, this looks like something interesting
  int psnum = tr.subnum;
  BotPathNode otherNode = nodes[psnum];
  twoWayRefresh.put(psnum, true);

  /+
  // refresh other node (just in case)
  refreshNovePassability(otherNode);

  if (!otherNode.passable) {
    /*if (tr.dist != -1)*/ {
      print("invalidated exit #%d from node #%d to sub #%d due to other-invalid", exitnum, node.index, tr.subnum);
      tr.dist = -1;
      tr.needJump = false;
      tr.needUse = false;
      tr.moveCost = -1;
    }
    return;
  }
  +/

  EntityEx::PathInfo pi;
  TVec dir = otherNode.Origin-node.Origin;
  dir.z = 0;
  float dist = dir.length2D;
  dir = dir.normalise2D;

  float yaw = VectorAngleYaw(dir);
  bool reached = botmo.TracePath(yaw, dist, out pi, allowJump:true, allowDrop:true, ignoreMonsters:true, srcOrigin:node.Origin);
  // if not reached, but the destination is in required subsector, consider this node reachable
  if (!reached) {
    TVec res = node.Origin+dir*pi.maxdist;
    GameObject::subsector_t *dss = lev.PointInSubsector(res);
    if (dss == tr.sub) reached = true;
  }

  // if reached, record this path
  if (reached) {
    //if (dumpReachable && tr.dist < 0) print("  can reach sub #%d from sub #%d", tr.subnum, node.index);
    bool wasValid = (tr.dist >= 0);
    tr.dist = pi.maxdist;
    tr.needJump = pi.needJump;
    tr.needUse = pi.needUse;
    tr.moveCost = pi.maxdist*pi.maxdist+(pi.needUse ? 20 : 0);
    if (bot.IsDangerous(tr.sub.sector)) tr.moveCost *= 9;
    /*if (!wasValid)*/ print("validated exit #%d from node #%d to sub #%d", exitnum, node.index, tr.subnum);
  } else {
    /*if (tr.dist != -1)*/ {
      tr.dist = -1;
      tr.needJump = false;
      tr.needUse = false;
      tr.moveCost = -1;
      print("invalidated exit #%d from node #%d to sub #%d", exitnum, node.index, tr.subnum);
    }
  }
}


final void refreshNode (BotPathNode node) {
  if (node.queryCounter == queryCounter) return;
  refreshNovePassability(node);
  foreach (auto tridx, ref auto tr; node.exits) refreshNodeExit(node, tridx);
}


transient array!int tmpNodeInds;

final void refreshAllNodesInMap () {
  if (refreshMap.length == 0) return;
  Reset();

  twoWayRefresh.reset();
  for (auto idx = refreshMap.firstIndex(); refreshMap.isValidIndex(idx); idx = refreshMap.nextIndex(idx)) {
    int key = refreshMap.keyAtIndex(idx);
    auto node = nodes[key];
    node.queryCounter = 0;
    print("refreshing node #%d (sector #%d)", node.index, node.sub.sector-&bot.player.MO.XLevel.Sectors[0]);
    refreshNode(node);
  }

  refreshMap.reset();

  // now refresh extra nodes
  while (twoWayRefresh.length) {
    tmpNodeInds.reset();
    for (auto idx = twoWayRefresh.firstIndex(); twoWayRefresh.isValidIndex(idx); idx = twoWayRefresh.nextIndex(idx)) {
      int key = twoWayRefresh.keyAtIndex(idx);
      auto node = nodes[key];
      if (node.queryCounter != queryCounter || node.isSectorChanged) {
        node.queryCounter = 0;
        tmpNodeInds[$] = key;
      }
    }
    twoWayRefresh.reset();
    if (!tmpNodeInds.length) break;
    foreach (auto nidx; tmpNodeInds) {
      auto node = nodes[nidx];
      print("secondary refreshing node #%d (sector #%d)", node.index, node.sub.sector-&bot.player.MO.XLevel.Sectors[0]);
      refreshNode(node);
    }
  }
}


final void Setup (BotPlayerBase abot) {
  if (!abot) FatalError("something is very wrong with bot pathfinder");
  bot = abot;
  foreach (ref auto node; nodes) delete node;
  EntityEx botmo = EntityEx(bot.player.MO);
  Level lev = botmo.XLevel;

  Reset();
  queryCounter = 1;
  refreshMap.reset();

  EntityEx::FakeDoorInfo savedPlane;

  // create nodes
  nodes.length = lev.Subsectors.length;
  foreach (auto idx, ref auto node; nodes) {
    GameObject::subsector_t *sub = &lev.Subsectors[idx];
    node = BotPathNode.CreateNew(lev);
    node.sub = sub;
    node.index = idx;
    TVec cpos = lev.GetSubsectorCenter(sub);
    cpos.z = EntityEx::ONFLOORZ;
    node.Origin = cpos;
    node.myFloorDist = float.max; //sub.sector.floor.dist;
    node.myCeilDist = float.max; //sub.sector.ceiling.dist;
    node.door = isDoorSector(sub.sector, sub);
    node.queryCounter = 0;
    node.passable = true;
    refreshNovePassability(node);
  }

  foreach (auto node; nodes) {
    // if this sector a door, fake-open it
    fakeOpenDoor(node, out savedPlane);
    scope(exit) restoreFakeOpenDoor(savedPlane);

    auto sub = node.sub;

    // create exits
    foreach (auto lidx; sub.firstline..sub.firstline+sub.numlines) {
      auto seg = &lev.Segs[lidx];
      if (!seg.partner || seg.partner.frontsub == seg.frontsub) continue; // not interesting

      GameObject::line_t *ld = seg.linedef;
      if (ld) {
        // not a miniseg
        if (!(ld.flags&GameObject::ML_TWOSIDED)) continue; // not interesting
        if (ld.flags&(GameObject::ML_BLOCKING|GameObject::ML_BLOCKPLAYERS|GameObject::ML_BLOCKEVERYTHING)) continue; // not interesting
      }

      auto tr = node.exits.alloc();
      tr.seg = seg;
      tr.sub = seg.partner.frontsub;
      tr.subnum = tr.sub-&lev.Subsectors[0];

      //if (tr.sub.sector != sub.sector) node.sectorExits[$] = node.exits.length-1;

      refreshNodeExit(node, node.exits.length-1);
    }
    twoWayRefresh.reset();
  }

  twoWayRefresh.reset();
}


// estimate distance between two nodes
// use squared distance, because it doesn't matter, so we can avoid `sqrt()` call
override float LeastCostEstimate (MiAStarNodeBase start, MiAStarNodeBase end) {
  BotPathNode s = BotPathNode(start);
  BotPathNode e = BotPathNode(end);
  queueRefreshNode(s);
  queueRefreshNode(e);
  if (debugDump) print("LeastCostEstimate from %d (%B) to %d (%B); est=%s (sc=%B:%B)", s.index, s.passable, e.index, e.passable, (s.Origin-e.Origin).length2DSquared, s.isSectorChanged, e.isSectorChanged);
  //if (!s.passable || !e.passable) return 999999;
  return (s.Origin-e.Origin).length2DSquared;
}


// should always generate the same set for the same node,
// so we cannot update nodes here
override void AdjacentCost (MiAStarNodeBase pathnode) {
  BotPathNode node = BotPathNode(pathnode);
  if (debugDump) print("AdjacentCost for %s (%B) (sc=%B)", node.index, node.passable, node.isSectorChanged);
  queueRefreshNode(node);
  //if (!node.passable) return;
  foreach (const ref auto tr; node.exits) {
    auto n2 = nodes[tr.subnum];
    auto sec = tr.sub.sector;
    if (sec.floor.dist != tr.floorDist || sec.ceiling.dist != tr.ceilDist) {
      queueRefreshNode(node, forced:true);
    }
    queueRefreshNode(n2);
    if (tr.moveCost >= 0 /*&& n2.passable*/) {
      if (debugDump) print("  for %s: adj is #%s (cost=%s)", node.index, tr.subnum, tr.moveCost);
      PushAdjacentCost(n2, tr.moveCost);
    } else {
      if (debugDump) print("  for %s: adj is #%s DROPPED", node.index, tr.subnum);
    }
  }
}


// returns `false` if there is no path
final bool BuildPath (TVec from, GameObject::subsector_t *fromsub, TVec dest, GameObject::subsector_t *destsub) {
  BotPlayerBase abot = bot;
  if (!abot || !abot.player || abot.player.Health <= 0) return false;
  // we may need alive bot mobile to re-evaluate nodes
  Actor botmo = Actor(abot.player.MO);
  if (!botmo || botmo.Health <= 0) return false; // dead bot cannot go anywhere
  Level lev = bot.player.MO.XLevel;

  if (!fromsub) fromsub = lev.PointInSubsector(from);
  if (!destsub) destsub = lev.PointInSubsector(dest);

  // early exit: same subsector is always reachable
  if (fromsub == destsub) {
    PathArrayClear();
    PathArrayPushNode(nodes[fromsub-&lev.Subsectors[0]]);
    pathCost = 0;
    return true;
  }

  queueRefreshNode(nodes[fromsub-&lev.Subsectors[0]]);
  queueRefreshNode(nodes[destsub-&lev.Subsectors[0]]);

  for (;;) {
    refreshAllNodesInMap();
    bool res = Solve(nodes[fromsub-&lev.Subsectors[0]], nodes[destsub-&lev.Subsectors[0]]);
    if (refreshMap.length == 0) return res;
    print("*** MAP CHANGED! ***");
  }
}


defaultproperties {
}
