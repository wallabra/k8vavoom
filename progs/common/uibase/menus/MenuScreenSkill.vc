//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2020 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, version 3 of the License ONLY.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class MenuScreenSkill : MenuScreenPicButtonsBase;


//==========================================================================
//
//  CreateChoices
//
//==========================================================================
override void CreateChoices () {
  foreach (auto i; 0..P_GetNumSkills()) {
    MenuChoiceSkill Btn = GetItemContainer().NewChild(MenuChoiceSkill);
    SkillDef *SDef = P_GetSkillDef(i);
    Btn.SetPos(ChoicesStartX, GetCurrentY());
    Btn.SetText(SDef->MenuName);
    if (SDef->bMenuNameIsPic) Btn.SetPicture(R_RegisterPic(name(SDef->MenuName)));
    foreach (auto j; 0..SDef->PlayerClassNames.length) {
      if (SDef->PlayerClassNames[j].ClassName.strEquCI(ClGame.SkillClassName)) {
        Btn.SetText(SDef->PlayerClassNames[j].MenuName);
      }
    }
    if (SDef->TextColor) Btn.SetText("\c["~SDef->TextColor~"]"~Btn.Text);
    FixButtonWidget(Btn); // this fixes height
    // set other button fields
    Btn.SkillIndex = i;
    Btn.bMustConfirm = SDef->bMustConfirm;
    Btn.ConfirmationText = SDef->ConfirmationText;
    Btn.Activated = &ActivateSkill;
  }
  FixMenuWidth();
}


//==========================================================================
//
//  SetDefaultChoice
//
//==========================================================================
override void SetDefaultChoice () {
  ::SetDefaultChoice();

  int goodSkill = max(0, P_GetNumSkills()-2);
  foreach (auto i; 0..P_GetNumSkills()) {
    auto sdef = P_GetSkillDef(i);
    if (sdef->bMustConfirm) {
      goodSkill = i-1;
      break;
    }
  }

  foreach (auto i; 0..goodSkill) CycleNextChoice();
}


//==========================================================================
//
//  ActivateSkill
//
//==========================================================================
void ActivateSkill (Object Sender) {
  MenuChoiceSkill Btn = MenuChoiceSkill(Sender);
  if (Btn.bMustConfirm) {
    ClGame.SkillToConfirm = Btn.SkillIndex;
    ClGame.MessageBoxShowSkillQuery(Btn.ConfirmationText);
  } else {
    ClGame.NewGame(Btn.SkillIndex);
  }
}


defaultproperties {
  MenuName = 'Skill_Menu';
  Title = "CHOOSE SKILL LEVEL:";
}
