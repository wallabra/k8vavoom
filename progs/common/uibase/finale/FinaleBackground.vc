//**************************************************************************
//**
//**    ##   ##    ##    ##   ##   ####     ####   ###     ###
//**    ##   ##  ##  ##  ##   ##  ##  ##   ##  ##  ####   ####
//**     ## ##  ##    ##  ## ##  ##    ## ##    ## ## ## ## ##
//**     ## ##  ########  ## ##  ##    ## ##    ## ##  ###  ##
//**      ###   ##    ##   ###    ##  ##   ##  ##  ##       ##
//**       #    ##    ##    #      ####     ####   ##       ##
//**
//**  Copyright (C) 1999-2006 Jānis Legzdiņš
//**  Copyright (C) 2018-2019 Ketmar Dark
//**
//**  This program is free software: you can redistribute it and/or modify
//**  it under the terms of the GNU General Public License as published by
//**  the Free Software Foundation, either version 3 of the License, or
//**  (at your option) any later version.
//**
//**  This program is distributed in the hope that it will be useful,
//**  but WITHOUT ANY WARRANTY; without even the implied warranty of
//**  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
//**  GNU General Public License for more details.
//**
//**  You should have received a copy of the GNU General Public License
//**  along with this program.  If not, see <http://www.gnu.org/licenses/>.
//**
//**************************************************************************
class FinaleBackground : Widget;

FinaleScreen FScreen;

int fpage1;
int fpage2;
int[7] fend;

int laststage;


//==========================================================================
//
//  InitPic
//
//==========================================================================
void InitPic (name PicName) {
  fpage1 = R_RegisterPic(PicName);
}


//==========================================================================
//
//  InitPicPal
//
//==========================================================================
void InitPicPal (name PicName, name PalName) {
  fpage1 = R_RegisterPicPal(PicName, PalName);
}


//==========================================================================
//
//  InitBunnyScroll
//
//==========================================================================
void InitBunnyScroll () {
  fpage1 = R_RegisterPic('pfub1');
  fpage2 = R_RegisterPic('pfub2');
  fend[0] = R_RegisterPic('end0');
  fend[1] = R_RegisterPic('end1');
  fend[2] = R_RegisterPic('end2');
  fend[3] = R_RegisterPic('end3');
  fend[4] = R_RegisterPic('end4');
  fend[5] = R_RegisterPic('end5');
  fend[6] = R_RegisterPic('end6');
}


//==========================================================================
//
//  InitDemon
//
//==========================================================================
void InitDemon () {
  fpage1 = R_RegisterPic('final1');
  fpage2 = R_RegisterPic('final2');
}


//==========================================================================
//
//  InitChess
//
//==========================================================================
void InitChess () {
  fpage1 = R_RegisterPic('finale1');
       if (FScreen.ClGame.maxclients > 1) fpage2 = R_RegisterPic('chessall');
  else if (GetCvar('class') == 1) fpage2 = R_RegisterPic('chessc');
  else if (GetCvar('class') == 2) fpage2 = R_RegisterPic('chessm');
}


//==========================================================================
//
//  OnDraw
//
//==========================================================================
override void OnDraw () {
       if (FScreen.FinaleType == 'EndGameBunny') DrawBunnyScroll();
  else if (FScreen.FinaleType == 'EndGameDemon') DrawDemonScroll();
  else if (FScreen.FinaleType == 'EndGameChess') DrawChess();
  else if (FScreen.FinaleType == 'EndGameCast') DrawFullScreenPicFinale(fpage1);
  //else if (FScreen.FinaleType == 'EndGameStrife') DrawFullScreenPic(fpage1);
  else DrawFullScreenPic(fpage1);
}


//==========================================================================
//
//  DrawBunnyScroll
//
//==========================================================================
void DrawBunnyScroll () {
  float scrolled = (FScreen.FinaleTime-6.5)*16.0;
  // just in case
  R_FillRect(0, 0, GetVirtualWidth(), GetVirtualHeight(), 0xff_000000);

  // fpage1 is the right page, fpage2 is the left page
       if (scrolled <= 0) DrawPicFS320HScroll2(1, fpage2, fpage1);
  else if (scrolled >= 320) DrawPicFS320HScroll2(0, fpage2, fpage1);
  else DrawPicFS320HScroll2(1.0-scrolled/320.0, fpage2, fpage1);

  if (FScreen.FinaleTime < 32.0) return;

  if (FScreen.FinaleTime < 35.0) {
    /*DrawPicFS320*/DrawFullScreenPicFinale320Ofs((320-13*8)/2, (200-8*8)/2, fend[0]);
    laststage = 0;
    return;
  }

  int stage = min(6, int((FScreen.FinaleTime-35.0)*7.0));
  if (stage > laststage) {
    LocalSound('weapons/pistol');
    laststage = stage;
  }
  /*DrawPicFS320*/DrawFullScreenPicFinale320Ofs((320-13*8)/2, (200-8*8)/2, fend[stage]);
}


//==========================================================================
//
//  DrawDemonScroll
//
//==========================================================================
void DrawDemonScroll () {
  // fpage1 is the right page, fpage2 is the left page
       if (FScreen.FinaleTime < 2.0) DrawPicFS200VScroll2(1, fpage2, fpage1);
  else if (FScreen.FinaleTime >= 22.0) DrawPicFS200VScroll2(0, fpage2, fpage1); // we'll just sit here and wait, for now
  else DrawPicFS200VScroll2(1.0-(FScreen.FinaleTime-2.0)/20.0, fpage2, fpage1);
}


//===========================================================================
//
//  DrawChess
//
//===========================================================================
void DrawChess () {
  //DrawPicFS320(0, 0, fpage1);
  DrawFullScreenPicFinale320(0, 0, fpage1);
  if (FScreen.FinaleStage == 4 || FScreen.FinaleStage == 5) {
    // chess pic, draw the correct character graphic
         if (FScreen.ClGame.maxclients > 1) DrawFullScreenPicFinale320(20, 0, fpage2, dontClearScreen:true);
    else if (GetCvar('class')) DrawFullScreenPicFinale320(60, 0, fpage2, dontClearScreen:true);
  }

  switch (FScreen.FinaleStage) {
    case 0: // fade in initial finale screen
      FadeIn();
      break;
    case 3: // fade screen out
      FadeOut();
      break;
    case 4: // fade in chess screen
      FadeIn();
      break;
  }
}


//==========================================================================
//
//  FadeOut
//
//==========================================================================
void FadeOut () {
  float fade = fclamp(FScreen.FinaleTime/FScreen.FinaleEndTime, 0.0, 0.99);
  //ShadeRect(0, 0, Width, Height, fade);
  auto ovw = GetVirtualWidth();
  auto ovh = GetVirtualHeight();
  scope(exit) SetVirtualScreen(ovw, ovh);
  SetVirtualScreen(320, 200);
  ShadeRect(0, 0, GetVirtualWidth, GetVirtualHeight, fade);
}


//==========================================================================
//
//  FadeIn
//
//==========================================================================
void FadeIn () {
  float fade = fclamp(1.0-FScreen.FinaleTime/FScreen.FinaleEndTime, 0.0, 0.99);
  //ShadeRect(0, 0, Width, Height, fade);
  auto ovw = GetVirtualWidth();
  auto ovh = GetVirtualHeight();
  scope(exit) SetVirtualScreen(ovw, ovh);
  SetVirtualScreen(320, 200);
  ShadeRect(0, 0, GetVirtualWidth, GetVirtualHeight, fade);
}


defaultproperties {
  Width = 320;
  Height = 200;
}
