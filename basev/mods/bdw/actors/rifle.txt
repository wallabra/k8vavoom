Actor BDW_RifleSpread : Inventory { inventory.maxamount 6 }


Actor BDW_RifleAmmo : Ammo {
  Inventory.Amount 0
  Inventory.MaxAmount 31
  Ammo.BackpackAmount 0
  Ammo.BackpackMaxAmount 31
  Inventory.Icon "RIFLA0"
}


Actor BDW_RifleClip : Ammo Replaces Clip {
  Game Doom
  SpawnID 11
  Inventory.PickupMessage "Picked up a Magazine."
  Inventory.Amount 10
  Inventory.MaxAmount 300
  Ammo.BackpackAmount 30
  Ammo.BackpackMaxAmount 600
  Inventory.Icon "CLIPA0"
  States {
    Spawn:
      CLIP A -1
      Stop
  }
}


Actor BDW_RifleClipBox : BDW_RifleClip Replaces Clipbox {
  Game Doom
  SpawnID 139
  Inventory.PickupMessage "Picked up a box of rifle ammunition."
  Inventory.Amount 100
  States {
    Spawn:
      AMMO A -1
      Stop
  }
}


Actor BDW_Rifle : Weapon Replaces Chaingun {
  Weapon.SlotNumber 2
  Weapon.AmmoUse1 0
  Weapon.AmmoGive1 10
  Weapon.AmmoUse2 0
  Weapon.AmmoGive2 0
  YScale 0.6
  XScale 0.8
  Weapon.SelectionOrder 4500
  Weapon.AmmoType1 "BDW_RifleClip"
  Weapon.AmmoType2 "BDW_RifleAmmo"
  Obituary "%o was shot down by %k's assault rifle."
  AttackSound "None"
  Inventory.PickupSound "weapons/bdw_rifle/clipin"
  Inventory.Pickupmessage "You got the Assault Rifle!"
  +WEAPON.NOAUTOAIM
  +WEAPON.NOALERT
  +FORCEXYBILLBOARD
  Scale 0.8

  var int user_Unloading;
  var int user_HasUnloaded;

  States {
    PickUp:
      TNT1 A 0
      Stop

    Steady:
      TNT1 A 1
      Goto Ready

    Ready:
      TNT1 A 2
      TNT1 A 0 A_PlaySoundEx("weapons/bdw_rifle/clipin", "Weapon")
      TNT1 A 0 A_SetUserVar("user_HasUnloaded", 1);
      TNT1 A 0 A_SetUserVar("user_Unloading", 0);
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, 2)
      TNT1 A 0 A_SetUserVar("user_HasUnloaded", 0);
      RIFS ABC 1

    OkToFire:
      TNT1 A 0 A_JumpIf(user_Unloading, "StartUnload")
      TNT1 A 0 A_TakeInventory("BDW_RifleSpread", 5)
      RIFG A 1 A_WeaponReady(WRF_ALLOWRELOAD)
      Goto OkToFire

    Deselect:
      RIFS CBA 1
      TNT1 AAAAAAAAAAAAAAAAAA 0 A_Lower
      TNT1 A 1
      Wait

    Select:
      TNT1 AAAAAAAAAAAAAAAAAAAAAAAAAA 0 A_Raise
      Goto Ready

    Reload:
      Goto StartReload

    Spawn:
      RIFL A -1
      Stop

    Fire:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "FireDo0")
      Goto StartReload

    FireDo0:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleSpread", 5, "FireHighSpread")
      TNT1 A 0 A_JumpIfInventory("BDW_RifleSpread", 3, "FireMidSpread")
      TNT1 A 0 A_GiveInventory("BDW_RifleSpread", 1)
      TNT1 A 0 A_PlaySound("weapons/bdw_rifle/shot")
      RIFF A 1 BRIGHT A_AlertMonsters
      TNT1 A 0 A_SpawnItemEx("BDW_PlayerMuzzle", 30, 5, 30)
      RIFF B 1 BRIGHT A_FireBullets(2, 2, 1, 9, "BDW_HitPuff")
      //RIFF A 0 //A_FireCustomMissile("BDW_Tracer", random(-1, 1), 0, -1, -12, 0, random(-1, 1))
      RIFF B 0 BRIGHT A_SetPitch(-1.3+pitch)
      TNT1 A 0 A_TakeInventory("BDW_RifleAmmo", 1)
      RIFF B 1 BRIGHT A_SetPitch(+0.6+pitch)
      RIFF C 0 A_FireCustomMissile("BDW_RifleCaseSpawn", 5, 0, 6, -6)
      RIFG A 1 A_SetPitch(+0.5+pitch)
      RIFG A 1 A_Refire
      RIFG A 4 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
      RIFG A 5 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD)
      Goto OkToFire

    FireMidSpread:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "FireMidSpreadDo0")
      Goto StartReload

    FireMidSpreadDo0:
      TNT1 A 0 A_GiveInventory("BDW_RifleSpread", 1)
      TNT1 A 0 A_PlaySound("weapons/bdw_rifle/shotfast")
      RIFF A 1 BRIGHT A_AlertMonsters
      TNT1 A 0 A_SpawnItemEx("BDW_PlayerMuzzle", 30, 5, 30)
      RIFF B 1 BRIGHT A_FireBullets(4, 4, 1, 9, "BDW_HitPuff")
      //RIFF A 0 //A_FireCustomMissile("BDW_Tracer", random(-2, 2), 0, -1, -12, 0, random(-2, 2))
      RIFF B 0 BRIGHT A_SetPitch(-1.0+pitch)
      TNT1 A 0 A_TakeInventory("BDW_RifleAmmo", 1)
      RIFF B 1 BRIGHT A_SetPitch(+0.5+pitch)
      RIFF C 0 A_FireCustomMissile("BDW_RifleCaseSpawn", 5, 0, 6, -6)
      RIFG A 1 A_SetPitch(+0.4+pitch)
      RIFG A 1 A_Refire
      RIFG A 4 A_Refire //A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
      RIFG A 5 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD)
      Goto OkToFire

    FireHighSpread:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "FireHighSpreadDo0")
      Goto StartReload

    FireHighSpreadDo0:
      TNT1 A 0 A_PlaySound("weapons/bdw_rifle/shotfast")
      RIFF A 1 BRIGHT A_AlertMonsters
      TNT1 A 0 A_SpawnItemEx("BDW_PlayerMuzzle", 30, 5, 30)
      RIFF B 1 BRIGHT A_FireBullets(7, 7, 1, 9, "BDW_HitPuff")
      //RIFF A 0 //A_FireCustomMissile("BDW_Tracer", random(-4, 4), 0, -1, -12, 0, random(-4, 4))
      RIFF B 0 BRIGHT A_SetPitch(-0.7+pitch)
      TNT1 A 0 A_TakeInventory("BDW_RifleAmmo", 1)
      RIFF B 1 BRIGHT A_SetPitch(+0.4+pitch)
      RIFF C 0 A_FireCustomMissile("BDW_RifleCaseSpawn", 5, 0, 6, -6)
      RIFG A 1 A_SetPitch(+0.3+pitch)
      RIFG A 1 A_Refire
      RIFG A 4 A_Refire //A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
      RIFG A 5 A_WeaponReady(WRF_NOBOB|WRF_ALLOWRELOAD)
      Goto OkToFire

    NoAmmo:
      TNT1 A 0 A_TakeInventory("BDW_RifleSpread", 5)
      RIFG A 1 A_PlaySoundEx("weapons/bdw_rifle/empty", "Weapon")
      Goto OkToFire


    StartReload:
      TNT1 A 0 A_TakeInventory("BDW_RifleSpread", 5)
      RIFG A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH)
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 31, "OkToFire")
      TNT1 A 0 A_JumpIfInventory("BDW_RifleClip", 1, "ReloadDoIt0")
      Goto NoAmmo

    ReloadDoIt0:
      TNT1 A 0 A_PlaySoundEx("weapons/bdw_rifle/reload", "Weapon")
      TNT1 A 0 A_JumpIf(user_HasUnloaded, "ReloadSkipEmptyClip")
      RIFR F 1 A_FireCustomMissile("BDW_EmptyClipSpawn", -5, 0, 8, -4)

    ReloadSkipEmptyClip:
      RIFR GGGGGGGG 1
      RIFR HIKL 1
      RIFR MMM 1
      TNT1 A 0 A_SetUserVar("user_HasUnloaded", 0)
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "InsertBullets2") //30+1 effect

    InsertBullets:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 31, "InsertBullets2")
      TNT1 A 0 A_JumpIfInventory("BDW_RifleClip", 1, "ReallyInsertBullets")
      Goto OkToFire

    ReallyInsertBullets:
      TNT1 A 0 A_GiveInventory("BDW_RifleAmmo", 1)
      TNT1 A 0 A_TakeInventory("BDW_RifleClip", 1)
      Goto InsertBullets

    InsertBullets2:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 31, "OkToFire")
      TNT1 A 0 A_JumpIfInventory("BDW_RifleClip", 1, "ReallyInsertBullets2")
      Goto OkToFire

    ReallyInsertBullets2:
      TNT1 A 0 A_GiveInventory("BDW_RifleAmmo", 1)
      TNT1 A 0 A_TakeInventory("BDW_RifleClip", 1)
      Goto InsertBullets2


    StartUnload:
      TNT1 A 0 A_TakeInventory("BDW_RifleSpread", 5)
      RIFG A 1 A_WeaponReady(WRF_NOBOB|WRF_NOFIRE|WRF_NOSWITCH)
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "StartUnloadDo0")
      Goto NoAmmo

    StartUnloadDo0:
      RIFR TSRQPO 1
      TNT1 A 0 A_PlaySoundEx("weapons/bdw_rifle/reload", "Weapon")
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)

    RemoveBullets:
      TNT1 A 0 A_JumpIfInventory("BDW_RifleAmmo", 1, "RemoveBulletsDo0")
      Goto FinishUnload

    RemoveBulletsDo0:
      TNT1 A 0 A_TakeInventory("BDW_RifleAmmo", 1)
      TNT1 A 0 A_GiveInventory("BDW_RifleClip", 1)
      Goto RemoveBullets

    FinishUnload:
      RIFR NMLKIGHGFEDCBA 1
      TNT1 A 0 A_PlaySoundEx("weapons/bdw_rifle/empty", "Weapon")
      TNT1 A 0 A_SetUserVar("user_HasUnloaded", 1)
      TNT1 A 0 A_SetUserVar("user_Unloading", 0)
      Goto OkToFire
  }
}
